
literal : /[a-zA-Z]+[a-zA-Z0-9]*/

path : /[a-zA-Z0-9_\/\.]+/ 
name : literal
qualified_name : name ("." name)*

integer       : SIGNED_INT
quoted_string : ESCAPED_STRING
floating      : SIGNED_FLOAT
override      : "override" 
abstract      : "abstract" 
final         : "final"
none          : "None"
true          : "True"
false         : "False"
boolean       : true | false
modifier      : override | abstract | final

value: multi_assign
     | qualified_name
     | floating
     | integer
     | quoted_string
     | boolean
     | none

single_import : "from" path "import" qualified_name "as" name
multi_import : single_import*

smpl_assign : name ":" value
mdfr_assign : modifier name ":" value
xtnd_assign : name "extends" name ":" value
xtnd_mdfr_assign : modifier name "extends" name ":" value

single_assign : smpl_assign
              | mdfr_assign
              | xtnd_assign
              | xtnd_mdfr_assign

multi_assign : "{" single_assign ("," single_assign)* "}"

start : multi_import single_assign

%import common.ESCAPED_STRING
%import common.SIGNED_FLOAT
%import common.SIGNED_INT
%import common.WS
%ignore WS


